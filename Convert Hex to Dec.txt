Đây là đoạn code Tôi vừa mới viết theo yêu cầu của ban Thanhlam1911. Tôi đã mô phỏng thử, nó chuyển đổi rất chính xác:
;DOAN CODE CHUYEN DOI TH0_TL0 RA BCD TREN CAC BYTE 40H,41H,42H,43H,44H
; KHAI BAO
THUONG_HIGH EQU 7FH
THUONG_LOW EQU 7EH
SBC_HIGH EQU 7DH
SBC_LOW EQU 7CH
SO_DU EQU 7BH
;---------------------
;CHUONG TRINH CHINH
ORG 0000H
SJMP MAIN
ORG 0030H
MAIN: CALL HEX_DEC
SJMP MAIN

;----------------
;DOAN CHUONG CON

HEX_DEC: MOV SBC_HIGH,TH0
MOV SBC_LOW,TL0
CALL CHIA_10
MOV 40H,SO_DU ; BYTE CHUA CO SO THAP NHAT
MOV SBC_HIGH,THUONG_HIGH
MOV SBC_LOW,THUONG_LOW
CALL CHIA_10
MOV 41H,SO_DU ; BYTE CHUA CO SO THU 2
MOV SBC_HIGH,THUONG_HIGH
MOV SBC_LOW,THUONG_LOW
CALL CHIA_10
MOV 42H,SO_DU ; BYTE CHUA CO SO THU 3
MOV SBC_HIGH,THUONG_HIGH
MOV SBC_LOW,THUONG_LOW
CALL CHIA_10
MOV 43H,SO_DU ; BYTE CHUA CO SO THU 4
MOV SBC_HIGH,THUONG_HIGH
MOV SBC_LOW,THUONG_LOW
CALL CHIA_10
MOV 44H,SO_DU ; BYTE CHUA CO SO CAO NHAT
RET

;--------------
;DOAN CODE CHIA 10
; SO BI CHIA CHUA TREN SBC_HIGH VAF SBC_LOW
;THUONG SO CHUA TREN THUONG_HIGH VA THUONG_LOW
; SO DU CHUA TREN SO_DU
CHIA_10: MOV A,SBC_HIGH
MOV B,#10
DIV AB
MOV THUONG_HIGH,A

MOV A,B ; B DANG CHUA SO DU < 10
SWAP A
MOV B,A

MOV A,SBC_LOW
SWAP A
ANL A,#0FH ; CHI LAY NIBLE CAO CUA SBC_LOW GHEP VAO SO DU LA NIBLE CAO DANG O B
ORL A,B ; TAO DUOC 1 BYTE DE CHIA TIEP
MOV B,#10
DIV AB ; KET QUA PHEP CHIA NAY CHI LA MOT SO < 10

SWAP A
MOV THUONG_LOW,A ; DUA KET QUA VAO NIBLE CAO CUA THUONG_LOW
MOV A,B
SWAP A
MOV B,A
MOV A,SBC_LOW
ANL A,#0FH ; CHI LAY NIBLE THAP CUA SBC_LOW GHEP VAO SO DU LA NIBLE CAO DANG O B
ORL A,B ; TAO DUOC 1 BYTE DE CHIA TIEP
MOV B,#10
DIV AB
ORL A,THUONG_LOW
MOV THUONG_LOW,A
MOV SO_DU,B
RET
;---------------
END
Chúc vui!